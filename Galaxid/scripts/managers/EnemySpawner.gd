extends Node

# Klasa odpowiedzialna za tworzenie wrogów i random spawn

var level_manager: Node2D
var _scene_cache: Dictionary = {}  # enemy_id_str -> PackedScene | null

# Dane scrollingu i mapy
var back_move: int = 1
var back_move3: int = 3
var small_enemy_adjust: bool = false

# Random spawn system
var enemies_active: bool = false
var level_enemy_frequency: int = 96
var level_enemies: Array = []

# Offset X dla wszystkich wrogów (np. dla przesunięcia mapy)
var x_offset: float = 24

func _init(p_level_manager: Node2D):
	level_manager = p_level_manager

func set_scroll_data(p_back_move: int, p_back_move3: int):
	back_move = p_back_move
	back_move3 = p_back_move3

func set_small_enemy_adjust(active: bool):
	small_enemy_adjust = active

func set_random_spawn_data(p_level_enemies: Array, p_level_enemy_frequency: int = 96):
	level_enemies = p_level_enemies
	level_enemy_frequency = p_level_enemy_frequency

func set_enemies_active(p_active: bool):
	enemies_active = p_active

func process_random_spawn(_delta: float):
	if not enemies_active or level_enemies.is_empty():
		return

	if randi() % 100 > level_enemy_frequency:
		var enemy_id = level_enemies[randi() % level_enemies.size()]
		var enemy = _instantiate(enemy_id)
		if not enemy:
			return

		var spawn_x = enemy.startx
		if enemy.startxc != 0:
			spawn_x = enemy.startx + (randi() % (enemy.startxc * 2)) - enemy.startxc + 1

		var spawn_pos = Vector2(float(spawn_x), float(enemy.starty))

		_setup_enemy(enemy, enemy_id, spawn_pos,
			Vector2(float(enemy.xmove), float(enemy.ymove)),
			0, back_move, 0, 0, 25)

		enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
		level_manager.add_child(enemy)

func spawn_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var spawn_pos = Vector2(float(event.get("screen_x", 0)), float(event.get("screen_y", 0)))
	if small_enemy_adjust and enemy.esize == 0:
		spawn_pos.x -= 10
		spawn_pos.y -= 7

	var enemy_slot = int(event.get("enemy_slot", 25))
	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		int(event.get("event_type", 0)), int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_free_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	var vel = Vector2(
		float(event.get("vel_x", float(enemy.xmove))),
		float(event.get("vel_y", float(enemy.ymove))))

	_setup_enemy(enemy, enemy_id, spawn_pos, vel, 0, 0, 0, int(event.get("link_num", 0)), 0)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)
	print("Spawn Free Enemy")

func spawn_path_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 900))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	if event.has("path") and "wybran_sciezka" in enemy:
		enemy.wybran_sciezka = event.get("path")

	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	if enemy.has_signal("projectile_spawned"):
		_setup_enemy(enemy, enemy_id, spawn_pos, Vector2.ZERO, 0, 0, 100, 0, 25)
		enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	else:
		enemy.name = "Enemy_%03d" % enemy_id
		enemy.global_position = spawn_pos

	level_manager.add_child(enemy)

func just_spawn_enemy(event: Dictionary):
	if "path" in event:
		spawn_path_enemy(event)
	else:
		spawn_free_enemy(event)

func spawn_group_enemy(event: Dictionary):
	var group_id = int(event.get("enemy_id", 0))
	var base_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))
	base_pos.x += x_offset
	var link_num = int(event.get("link_num", 0))

	var group_scene = _scene_for_enemy(group_id)
	if not group_scene:
		return

	var group = group_scene.instantiate()

	var id_regex = RegEx.new()
	id_regex.compile("^Enemy_(\\d+)")

	for child in group.get_children():
		if not child is Node2D:
			continue
		var rx = id_regex.search(child.name)
		if not rx:
			continue

		var child_id = int(rx.get_string(1))
		var child_pos = base_pos + child.position

		var sub_event: Dictionary = {
			"enemy_id": child_id,
			"screen_x": child_pos.x,
			"screen_y": child_pos.y,
			"link_num": link_num,
		}

		if "wybran_sciezka" in child and child.wybran_sciezka != "":
			sub_event["path"] = child.wybran_sciezka
		else:
			sub_event["vel_x"] = float(event.get("vel_x", child.xmove))
			sub_event["vel_y"] = float(event.get("vel_y", child.ymove))

		just_spawn_enemy(sub_event)

	group.queue_free()

func spawn_formation(event: Dictionary):
	var formation_id = int(event.get("enemy_id", 0))
	var base_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))
	base_pos.x += x_offset
	var link_num = int(event.get("link_num", 0))

	var formation_scene = _scene_for_enemy(formation_id)
	if not formation_scene:
		return

	var formation = formation_scene.instantiate()
	formation.position = base_pos

	# Zbierz wrogów kontrolowanych przez RemoteTransform2D (ruch po ścieżce)
	var path_controlled: Array = []
	for rt in formation.find_children("*", "RemoteTransform2D", true, false):
		if rt.remote_path:
			var target = rt.get_node_or_null(rt.remote_path)
			if target:
				path_controlled.append(target)

	var id_regex = RegEx.new()
	id_regex.compile("^Enemy_(\\d+)")

	for child in formation.get_children():
		if not child is Node2D:
			continue
		var rx = id_regex.search(child.name)
		if not rx:
			continue
		if not child.has_signal("projectile_spawned"):
			continue

		var child_id = int(rx.get_string(1))
		child.enemy_id = child_id
		child.link_num = link_num
		child.enemy_slot = 0
		child.event_type = 0
		child.fixed_move_y = 0
		child.scroll_y = 0
		child.projectile_scene = GameConstants.enemy_projectile_scene
		child.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)

		if child in path_controlled:
			child.velocity = Vector2.ZERO
		else:
			child.velocity = Vector2(
				float(event.get("vel_x", child.xmove)),
				float(event.get("vel_y", child.ymove)))

	level_manager.add_child(formation)

func spawn_4x4_enemies(event: Dictionary):
	var enemy_ids = event.get("enemy_ids", [])
	var enemy_slot = int(event.get("enemy_slot", 25))
	var fixed_move_y = int(event.get("fixed_move_y", 0))
	var event_type = int(event.get("event_type", 0))
	var scroll_for_slot = _scroll_for_slot(enemy_slot)

	var base_pos = Vector2(
		float(event.get("screen_x", 0)) -24.0,
		float(event.get("screen_y", 0)) + 3.0 - 28.0)

	# Offsety dla 4x4 gridu (24x28px)
	var offsets = [Vector2(0, 26), Vector2(23, 26), Vector2(0, 0), Vector2(23, 0)]

	for i in range(min(4, enemy_ids.size())):
		var eid = int(enemy_ids[i])
		var enemy = _instantiate(eid)
		if not enemy:
			continue

		var spawn_pos = base_pos + offsets[i]
		if small_enemy_adjust and enemy.esize == 0:
			spawn_pos.x -= 10
			spawn_pos.y -= 7

		_setup_enemy(enemy, eid, spawn_pos,
			_velocity(enemy, int(event.get("y_vel", 0))),
			fixed_move_y, scroll_for_slot,
			event_type, int(event.get("link_num", 0)), enemy_slot)

		enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
		level_manager.add_child(enemy)
		
func spawn_free_4x4(event: Dictionary):
	var enemy_ids = event.get("enemy_ids", [])
	var base_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))
	# Offsety dla 4x4 gridu (24x28px)
	var offsets = [Vector2(0, 26), Vector2(23, 26), Vector2(0, 0), Vector2(23, 0)]

	for i in range(4):
		var eid = int(enemy_ids[i])
		var enemy = _instantiate(eid)
		if not enemy:
			continue

		var spawn_pos = base_pos + offsets[i]
		var vel = Vector2(
			float(event.get("vel_x", float(enemy.xmove))),
			float(event.get("vel_y", float(enemy.ymove))))

		_setup_enemy(enemy, eid, spawn_pos, vel, 0, 0, 0, int(event.get("link_num", 0)), 0)

		enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
		level_manager.add_child(enemy)

func spawn_sky_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))
	var enemy_slot = int(event.get("enemy_slot", 25))

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		int(event.get("event_type", 0)), int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)
	# print("spawned sky enemy", enemy_id)

func spawn_top_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 50))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		7, int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_ground_enemy(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 25))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		6, int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_ground_enemy_2(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 75))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	if small_enemy_adjust and enemy.esize == 0:
		spawn_pos.x -= 10
		spawn_pos.y -= 7

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		10, int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_sky_bottom(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 0))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), -int(event.get("back_move2", back_move)),
		int(event.get("event_type", 0)), int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_sky_bottom2(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 50))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		int(event.get("event_type", 0)), int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_ground2_bottom(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 75))
	var spawn_pos = Vector2(
		float(event.get("screen_x", 0)),
		float(event.get("screen_y", 0)))

	if small_enemy_adjust and enemy.esize == 0:
		spawn_pos.x -= 10
		spawn_pos.y -= 7

	_setup_enemy(enemy, enemy_id, spawn_pos,
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), _scroll_for_slot(enemy_slot),
		56, int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

func spawn_enemy_special(event: Dictionary):
	var enemy_id = int(event.get("enemy_id", 0))
	var enemy = _instantiate(enemy_id)
	if not enemy:
		return

	var enemy_slot = int(event.get("enemy_slot", 50))

	_setup_enemy(enemy, enemy_id, Vector2(float(event.get("screen_x", 0)), 190.0),
		_velocity(enemy, int(event.get("y_vel", 0))),
		int(event.get("fixed_move_y", 0)), -back_move3,
		32, int(event.get("link_num", 0)), enemy_slot)

	enemy.projectile_spawned.connect(level_manager._on_enemy_projectile_spawned)
	level_manager.add_child(enemy)

# ============================================================================
# Funkcje pomocnicze
# ============================================================================

func _instantiate(enemy_id: int) -> Node2D:
	var scene = _scene_for_enemy(enemy_id)
	if not scene:
		push_error("EnemySpawner: Brak sceny dla enemy_id=%d" % enemy_id)
		return null
	return scene.instantiate()

func _setup_enemy(enemy: Node2D, enemy_id: int, spawn_position: Vector2,
		velocity: Vector2, fixed_move_y: int, scroll_y: int,
		event_type: int, link_num: int, enemy_slot: int) -> void:
	enemy.name           = "Enemy_%d" % enemy_id
	enemy.global_position = spawn_position + Vector2(x_offset, 0)
	enemy.velocity       = velocity
	enemy.fixed_move_y   = fixed_move_y
	enemy.scroll_y       = scroll_y
	enemy.enemy_id       = enemy_id
	enemy.event_type     = event_type
	enemy.link_num       = link_num
	enemy.enemy_slot     = enemy_slot
	enemy.projectile_scene = GameConstants.enemy_projectile_scene

func _velocity(enemy: Node2D, y_vel: int) -> Vector2:
	return Vector2(float(enemy.xmove), float(enemy.ymove + y_vel))

func _scene_for_enemy(enemy_id: int) -> PackedScene:
	var key = "%03d" % enemy_id
	if _scene_cache.has(key):
		return _scene_cache[key]
	var path = "res://scenes/enemies/Enemy_%s.tscn" % key
	var scene = load(path) if ResourceLoader.exists(path) else null
	_scene_cache[key] = scene
	return scene

func _scroll_for_slot(enemy_slot: int) -> int:
	match enemy_slot:
		0:        return 0
		25, 75:   return back_move
		50:       return back_move3
		_:        return 0
